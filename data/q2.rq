prefix an: <http://www.adamnet.nl/>
prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
select * {
  ?organizatie an:woonplaats ?woonplaatsNaam ;
               an:straat ?straatNaam ;
               an:huisnummer ?huisnummer .
  service <https://data.pdok.nl/sparql> {
    ?openbareRuimte bag:naamOpenbareRuimte ?straatNaam ;
                    bag:bijbehorendeWoonplaats ?woonplaats .
    ?woonplaats bag:naamWoonplaats ?woonplaatsNaam .
    ?nummeraanduiding bag:bijbehorendeOpenbareRuimte ?openbareRuimte ;
                      bag:huisnummer ?huisnummer .
    ?verblijfsobject bag:hoofdadres ?nummeraanduiding ;
                     bag:oppervlakte ?oppervlakte ;
                     bag:pandrelatering ?pand ;
                     bag:status ?verblijfsobjectStatus .
    ?verblijfsobjectStatus rdfs:label ?verblijfsobjectStatusLabel .
    ?pand bag:oorspronkelijkBouwjaar ?bouwjaar ;
          bag:geometriePand/geo:asWKT ?wkt ;
          bag:status ?pandStatus .
    ?pandStatus rdfs:label ?pandStatusLabel
  }
  bind (concat('<h4>',str(?straatNaam),' ',str(?huisnummer),'</h4><h5><a href="',str(?verblijfsobject),'">Verblijfsobject</a>:</h5><ul><li>Woonplaats: <a href="',str(?woonplaats),'">',str(?woonplaatsNaam),'</a></li><li>Oppervlakte: ',str(?oppervlakte),'mÂ²</li><li>Status: <a href="',str(?verblijfsobjectStatus),'">',str(?verblijfsobjectStatusLabel),'</a></li></ul><h5><a href="',str(?pand),'">Pand</a>:</h5><ul><li>Bouwjaar: ',str(?bouwjaar),'</li><li>Status: <a href="',str(?pandStatus),'">',str(?pandStatusLabel),'</a></li></ul>') as ?wktLabel)
}
limit 1
