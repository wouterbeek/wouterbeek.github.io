PREFIX type: <http://info.deepcarbon.net/schema/type#>
prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
select ?pandShape ?pandShapeHeight ?pandShapeLabel ?pandShapeName {
  bind(11 as ?huisnummer)
  bind("9405VJ"^^xsd:string as ?postcode)
  service <https://data.pdok.nl/sparql> {
    ?woonplaats bag:identificatiecode ?woonplaatsId;
                rdfs:label ?woonplaatsNaam.
    graph ?g1 {
      ?openbareRuimte bag:bijbehorendeWoonplaats ?woonplaats;
                      bag:identificatiecode ?openbareRuimteId;
                      bag:naamOpenbareRuimte ?openbareRuimteNaam;
                      bag:status ?openbareRuimteStatus.
    }
    ?openbareRuimteStatus rdfs:label ?openbareRuimteStatusNaam.
    filter not exists { ?g1 bag:eindGeldigheid ?eind1. }
    graph ?g2 {
      ?nummeraanduiding bag:bijbehorendeOpenbareRuimte ?openbareRuimte;
                        bag:huisnummer ?huisnummer;
                        bag:identificatiecode ?nummeraanduidingId;
                        bag:postcode ?postcode.
    }
    filter not exists { ?nummeraanduiding bag:huisletter ?letter. }
    filter not exists { ?g2 bag:eindGeldigheid ?eind2. }
    graph ?g3 {
      ?verblijfsobject a ?c;
                       bag:hoofdadres ?nummeraanduiding;
                       bag:identificatiecode ?verblijfsobjectId;
                       bag:oppervlakte ?oppervlakte;
                       bag:pandrelatering ?pand;
                       bag:status ?verblijfsobjectStatus.
    }
    ?verblijfsobjectStatus rdfs:label ?verblijfsobjectStatusNaam.
    filter not exists { ?g3 bag:eindGeldigheid ?eind3. }
    filter(?c != bag:Verblijfsobject)
    ?c rdfs:label ?cLabel.
    graph ?g4 {
      ?pand geo:hasGeometry/geo:asWKT ?pandShape;
            bag:identificatiecode ?pandId;
            bag:status ?pandStatus;
            bag:oorspronkelijkBouwjaar ?bouwjaar.
    }
    ?pandStatus rdfs:label ?pandStatusNaam.
    filter not exists { ?g4 bag:eindGeldigheid ?eind4. }
  }
  bind(30 as ?pandShapeHeight)
  bind(concat(
    str(?openbareRuimteNaam),' ',str(?huisnummer),', ',
    str(?postcode),' ',str(?woonplaatsNaam)) as ?pandShapeName)
  bind(concat(
    '<h2>',str(?pandShapeName),'</h2>',
    '<h3>Kadastrale links</h3>',
    '<ul>',
      '<li><a href="',str(?pand),'" target="_blank">Pand ',str(?pandId),'</a></li>',
      '<li><a href="',str(?verblijfsobject),'" target="_blank">Verblijfsobject ',str(?verblijfsobjectId),'</a></li>',
      '<li><a href="',str(?nummeraanduiding),'" target="_blank">Nummeraanduiding ',str(?nummeraanduidingId),'</a></li>',
      '<li><a href="',str(?openbareRuimte),'" target="_blank">Openbare ruimte ',str(?openbareRuimteId),'</a></li>',
      '<li><a href="',str(?woonplaats),'" target="_blank">Woonplaats ',str(?woonplaatsId),'</a></li>',
    '</ul>',
    '<h3>Eigenschappen</h3>',
    '<ul>',
      '<li>Bouwjaar: ',str(?bouwjaar),'</li>',
      '<li>Verblijfsobject type: ',str(?cLabel),'</li>',
      '<li>Oppervlakte: ',str(?oppervlakte),'m2</li>',
      '<li>Status openbare ruimte: ',str(?openbareRuimteStatusNaam),'</li>',
      '<li>Status pand: ',str(?pandStatusNaam),'</li>',
      '<li>Status verblijfsobject: ',str(?verblijfsobjectStatusNaam),'</li>',
    '</ul>') as ?pandShapeLabel)
}
limit 5
