prefix : <http://www.adamnet.nl/>
prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
select * {
  ?organizatie
    :woonplaats ?woonplaatsNaam;
    :straat ?straatNaam;
    :huisnummer ?huisnummer.
  service <https://data.pdok.nl/sparql> {
    ?openbareRuimte
      bag:naamOpenbareRuimte ?straatNaam;
      bag:bijbehorendeWoonplaats ?woonplaats.
    ?woonplaats bag:naamWoonplaats ?woonplaatsNaam.
    ?nummeraanduiding
      bag:bijbehorendeOpenbareRuimte ?openbareRuimte;
      bag:huisnummer ?huisnummer.
    ?verblijfsobject
      bag:hoofdadres ?nummeraanduiding;
      bag:oppervlakte ?oppervlakte;
      bag:pandrelatering ?pand;
      bag:status ?verblijfsobjectStatus.
    ?verblijfsobjectStatus rdfs:label ?verblijfsobjectStatusLabel.
    ?pand
      bag:oorspronkelijkBouwjaar ?bouwjaar;
      bag:geometriePand/geo:asWKT ?shape;
      bag:status ?pandStatus.
    ?pandStatus rdfs:label ?pandStatusLabel.
  }
  bind ('''
        <h4>{{straatNaam}} {{huisnummer}}</h4>
        <h5><a href="{{verblijfsobject}}">Verblijfsobject</a>:</h5>
        <ul>
          <li>Woonplaats: <a href="{{woonplaats}}">{{woonplaatsNaam}}</a></li>
          <li>Oppervlakte: {{oppervlakte}}mÂ²</li>
          <li>Status: <a href="{{verblijfsobjectStatus}}">{{verblijfsobjectStatusLabel}}</a></li>
        </ul>
        <h5><a href="{{pand}}">Pand</a>:</h5>
        <ul>
          <li>Bouwjaar: {{bouwjaar}}</li>
          <li>Status: <a href="{{pandStatus}}">{{pandStatusLabel}}</a></li>
        </ul>
        ''' as ?shapeLabel)
}
limit 1
